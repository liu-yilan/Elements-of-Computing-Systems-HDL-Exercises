// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // if (zx == 1) sets xIntermediate = 000....000 
    Mux16(a=x, b=false, sel=zx, out=xIntermediate); 

    /*----------------------------------------------------------------------------*/
    // if (nx == 1) sets xFinal = !xIntermediate 
    Not16(in=xIntermediate, out=notXIntermediate);
    Mux16(a=xIntermediate, b=notXIntermediate, sel=nx, out=xFinal); 

    /*----------------------------------------------------------------------------*/
    // if (zy == 1) sets yIntermediate = 000....000 
    Mux16(a=y, b=false, sel=zy, out=yIntermediate);

    /*----------------------------------------------------------------------------*/
    // if (ny == 1) sets yFinal = !yIntermediate
    Not16(in=yIntermediate, out=notyIntermediate); 
    Mux16(a=yIntermediate, b=notyIntermediate, sel=ny, out=yFinal);

    /*----------------------------------------------------------------------------*/
    // if (f == 0)  sets out = x & y  // bitwise and
    // if (f == 1)  sets out = x + y  // integer 2's complement addition
    And16(a=xFinal, b=yFinal, out=xAy); 
    Add16(a=xFinal, b=yFinal, out=xPy); 
    Mux16(a=xAy, b=xPy, sel=f, out=resultFromF); 

    /*----------------------------------------------------------------------------*/
    // if (no == 1) sets out = !out   // bitwise not
    Not16(in=resultFromF, out=notResultFromF); 

    Mux16(a=resultFromF, b=notResultFromF, sel=no, out=out); 
    
    /*----------------------------------------------------------------------------*/
    // calculating ng
    Mux16(a=resultFromF, b=notResultFromF, sel=no, out[15]=ng);

    // calculating zr
    Mux16(a=resultFromF, b=notResultFromF, sel=no, out[0..7]=out0thru7); 
    Mux16(a=resultFromF, b=notResultFromF, sel=no, out[8..15]=out8thru15); 

    Or8Way(in=out0thru7, out=orOut0thru7); 
    Or8Way(in=out8thru15, out=orOut8thru15);
    Or(a=orOut0thru7, b=orOut8thru15, out=notzr); 
    Not(in=notzr, out=zr); 
}